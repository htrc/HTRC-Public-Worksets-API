prefix dcterms: <http://purl.org/dc/terms/>
prefix edm: <http://www.europeana.eu/schemas/edm/>
prefix htrc: <http://wcsa.htrc.illinois.edu/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ht: <https://www.hathitrust.org/>
prefix prism: <http://prismstandard.org/namespaces/basic/2.0/>
prefix as: <http://www.w3.org/ns/activitystreams#>
prefix bf: <http://id.loc.gov/ontologies/bibframe/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
# MUST REPLACE variables contained within $...$ before submitting SPARQL to Virtuoso RO-sparql end point
#   ws is Workset id
#   wsP is id of WorksetPage, which is Workset id extended with query string, i.e.: ?pageNo=x&pageSize=y
#   offset = (x-1)*y, limit = y, startIndex = offset+1
# Before returning result, MUST:
#   add link to context, http://worksets.hathitrust.org/dcWSfetch_resources/WorksetContext.jsonld, and flatten 
#   then add first, last, next, prev to WorksetPage description as appropriate (based in part on WS extent value)

CONSTRUCT {

  $wsP$  
	    rdf:type htrc:WorksetPage ;
	    as:startIndex $startIndex$ ;
	    as:partOf $ws$ ;
	    as:items ?vols .

  $ws$ 
        rdf:type htrc:Workset ;
		dcterms:title ?title ;
		dcterms:creator ?cre ; 
		dcterms:creator ?coll_cre ; 
		dcterms:created ?dat ;
		dcterms:extent  ?ext ;
		htrc:hasCriterion ?crit ;
		htrc:hasResearchMotivation ?mot ;
		htrc:intendedForUse ?intent ;
		htrc:hasVisibility ?vis ;
		htrc:hasGroup ?group ;
		dcterms:publisher ?pub ;
		dcterms:abstract ?abs ;
		dcterms:language ?lang ;
		dcterms:temporal ?temp ;
		dcterms:isVersionOf ?vers;
		dcterms:format ?form .
	  
  ?vols
		rdf:type ?vType;
		dcterms:isPartOf ?vColl;
		htrc:hasCatalogDescription ?vBib;
		htrc:hasFullDescription ?vFullJson;
		htrc:hasBriefDescription ?vBriefJson;
		htrc:mediatedBy ?vMediatedBy;
		dcterms:title ?vTitle;
		dcterms:creator ?vAuthor;
		dcterms:created ?vPubData;
		dcterms:accessRights ?vRights;
		ht:origin ?vOrigin;
		dcterms:contributor ?vContributor;
		dcterms:subject ?vSubject;
		prism:volume ?vVolume;
        bf:instanceOf ?bfWork ;
        htrc:WorkTitle ?bfWorkTitle ;
        htrc:WorkGenre ?bfGenre ;
        htrc:Title ?bfInstanceTitle ;
        htrc:AltTitles ?bfAltTitles ;
        bf:provisionActivity ?pubs .

    ?pubs
		htrc:pubYear ?bfPubDate ;
        htrc:imprint ?bfPubAgent ;
        htrc:pubLocation ?bfPubLoc .
	
}

where {
  $ws$
      rdf:type htrc:Workset ;
      dcterms:title ?title ;
      dcterms:created ?dat ;
      dcterms:extent  ?ext ;
      htrc:hasVisibility ?vis . 

  { select ?vols
      where {
        { select ?vols
           where { $ws$ edm:gathers ?vols . }
           ORDER BY ASC (?vols) } }
        OFFSET $offset$
        LIMIT $limit$            
  }
	  
	  OPTIONAL { $ws$ dcterms:abstract ?abs } 
	  OPTIONAL { $ws$ htrc:hasCriterion ?crit }
	  OPTIONAL { $ws$ htrc:hasResearchMotivation ?mot }
	  OPTIONAL { $ws$ htrc:hasGroup ?group }
	  OPTIONAL { $ws$ dcterms:creator ?cre_id .
               ?cre_id rdfs:label ?cre . }  
	  OPTIONAL { $ws$ dcterms:isVersionOf ?coll . 
               ?coll dcterms:creator ?coll_res . }
	  OPTIONAL { $ws$ dcterms:publisher ?pub }
	  OPTIONAL { $ws$ dcterms:temporal ?temp }
      OPTIONAL { $ws$ dcterms:language ?lang }	
      OPTIONAL { $ws$ dcterms:isVersionOf ?vers }
      OPTIONAL { $ws$ htrc:intendedForUse ?intent }		  
	  OPTIONAL { $ws$ dcterms:format ?form }
	  
	  OPTIONAL { ?vols rdf:type ?vType }
      OPTIONAL { ?vols dcterms:isPartOf ?vColl}
	  OPTIONAL { ?vols htrc:hasCatalogDescription ?vBib }
	  OPTIONAL { ?vols htrc:hasFullDescription ?vFullJson }
	  OPTIONAL { ?vols htrc:hasBriefDescription ?vBriefJson }
	  OPTIONAL { ?vols htrc:mediatedBy ?vMediatedBy }
	  OPTIONAL { ?vols dcterms:title ?vTitle }
	  OPTIONAL { ?vols dcterms:creator ?vAuthor }
	  OPTIONAL { ?vols dcterms:created ?vPubData }
	  OPTIONAL { ?vols dcterms:accessRights ?vRights }
	  OPTIONAL { ?vols ht:origin ?vOrigin }
	  OPTIONAL { ?vols dcterms:contributor ?vContributor }
	  OPTIONAL { ?vols dcterms:subject ?vSubject }
	  OPTIONAL { ?vols prism:volume ?vVolume }
      OPTIONAL { ?vols bf:instanceOf/bf:title/rdfs:label ?bfWorkTitle }
      OPTIONAL { ?vols rdfs:label ?bfInstanceTitle }
      OPTIONAL { ?vols bf:instanceOf ?bfWork }
      OPTIONAL { ?vols bf:instanceOf/bf:genreForm/rdfs:label ?bfGenre }
      OPTIONAL { ?vols bf:title/rdfs:label ?bfAltTitles }

      OPTIONAL { ?vols bf:provisionActivity ?pubs .
      	  OPTIONAL { ?vols bf:provisionActivity/bf:date ?bfPubDate }
	      OPTIONAL { ?vols bf:provisionActivity/bf:place/rdfs:label ?bfPubLoc }
	      OPTIONAL { ?vols bf:provisionActivity/bf:agent/rdfs:label ?bfPubAgent }
      }

      BIND(REPLACE(STRAFTER(STR(?coll_res),"http://catalogdata.library.illinois.edu/lod/entities/ht/user/"),"%20"," ") AS ?coll_cre) .
}