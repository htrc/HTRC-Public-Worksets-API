prefix dcterms: <http://purl.org/dc/terms/>
prefix edm: <http://www.europeana.eu/schemas/edm/>
prefix htrc: <http://wcsa.htrc.illinois.edu/> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ht: <https://www.hathitrust.org/>
prefix prism: <http://prismstandard.org/namespaces/basic/2.0/>
prefix as: <http://www.w3.org/ns/activitystreams#>
# MUST REPLACE variables contained within $...$ before submitting SPARQL to Virtuoso RO-sparql end point
#   ws is Workset id
#   wsP is id of WorksetPage, which is Workset id extended with query string, i.e.: ?pageNo=x&pageSize=y
#   offset = (x-1)*y, limit = y, startIndex = offset+1
# Before returning result, MUST:
#   add link to context, http://worksets.hathitrust.org/dcWSfetch_resources/WorksetContext.jsonld, and flatten 
#   then add first, last, next, prev to WorksetPage description as appropriate (based in part on WS extent value)

CONSTRUCT {

    <http://worksets.hathitrust.org/wsid/2122859244?pageNo=3&pageSize=10> 
	  rdf:type htrc:WorksetPage ;
	  as:startIndex 21 ;
	  as:partOf <http://worksets.hathitrust.org/wsid/2122859244> ;
	  as:items ?vols .

	<http://worksets.hathitrust.org/wsid/2122859244>   
		  rdf:type htrc:Workset ;
		  dcterms:title ?title ;
		  dcterms:creator ?cre ; 
		  dcterms:created ?dat ;
		  dcterms:extent  ?ext ;
		  htrc:hasCriterion ?crit ;
		  htrc:hasResearchMotivation ?mot ;
		  htrc:intendedForUse ?intent ;
		  htrc:hasVisibility ?vis ;
		  htrc:hasGroup ?group ;
		  dcterms:publisher ?pub ;
		  dcterms:abstract ?abs ;
		  dcterms:language ?lang ;
		  dcterms:temporal ?temp ;
		  dcterms:format ?form .
	  
  ?vols
		rdf:type ?vType;
		dcterms:isPartOf ?vColl;
		htrc:hasCatalogDescription ?vBib;
		htrc:hasFullDescription ?vFullJson;
		htrc:hasBriefDescription ?vBriefJson;
		htrc:mediatedBy ?vMediatedBy;
		dcterms:title ?vTitle;
		dcterms:creator ?vAuthor;
		dcterms:created ?vPubData;
		dcterms:accessRights ?vRights;
		ht:origin ?vOrigin;
		dcterms:contributor ?vContributor;
		dcterms:subject ?vSubject;
		prism:volume ?vVolume .
}

where {
  <http://worksets.hathitrust.org/wsid/2122859244> 
      rdf:type htrc:Workset ;
      dcterms:title ?title ;
      dcterms:creator ?cre ; 
      dcterms:created ?dat ;
      dcterms:extent  ?ext ;
      htrc:hasVisibility ?vis . 

  { select ?vols
      where {
        { select ?vols
           where { <http://worksets.hathitrust.org/wsid/2122859244> edm:gathers ?vols . }
           ORDER BY ASC (?vols) } }
        OFFSET 20
        LIMIT 10           
  }
	  
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> dcterms:abstract ?abs } 
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> htrc:hasCriterion ?crit }
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> htrc:hasResearchMotivation ?mot }
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> htrc:hasGroup ?group }
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> dcterms:publisher ?pub }
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> dcterms:temporal ?temp }
      OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> dcterms:language ?lang }	
      OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> htrc:intendedForUse ?intent }		  
	  OPTIONAL { <http://worksets.hathitrust.org/wsid/2122859244> dcterms:format ?form }
	  
	  OPTIONAL { ?vols rdf:type ?vType }
      OPTIONAL { ?vols dcterms:isPartOf ?vColl}
	  OPTIONAL { ?vols htrc:hasCatalogDescription ?vBib }
	  OPTIONAL { ?vols htrc:hasFullDescription ?vFullJson }
	  OPTIONAL { ?vols htrc:hasBriefDescription ?vBriefJson }
	  OPTIONAL { ?vols htrc:mediatedBy ?vMediatedBy }
	  OPTIONAL { ?vols dcterms:title ?vTitle }
	  OPTIONAL { ?vols dcterms:creator ?vAuthor }
	  OPTIONAL { ?vols dcterms:created ?vPubData }
	  OPTIONAL { ?vols dcterms:accessRights ?vRights }
	  OPTIONAL { ?vols ht:origin ?vOrigin }
	  OPTIONAL { ?vols dcterms:contributor ?vContributor }
	  OPTIONAL { ?vols dcterms:subject ?vSubject }
	  OPTIONAL { ?vols prism:volume ?vVolume }

}